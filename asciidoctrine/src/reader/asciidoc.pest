asciidoc = _{ (NEWLINE* ~ block)*  ~ NEWLINE* ~ EOI }

block = {
  header |
  // admonition |
  // example |
  // fenced |
  // listing |
  // literal |
  // open |
  // passthrough |
  // quote |
  // sidebar |
  // source |
  // stem |
  // table |
  // verse |
  image_block |
  include_macro |
  delimited_block |
  list |
  attribute_entry_block |
  // Title is nearly the last because it could prevent correct match of others
  title_block |
  // paragraph is the last because all others should be checked first
  paragraph |
  (!EOI ~ ANY)+
}

header = {
  title ~
  (NEWLINE ~ author_info)? ~
  (NEWLINE ~ revision_info)? ~
  ( (NEWLINE ~ attribute_entry) |
    (NEWLINE ~ "//" ~ (!EOI ~ !NEWLINE ~ ANY)* ~ &NEWLINE ) // TODO Comment entfernen
  )*
  ~ &NEWLINE{2,}
}

revision_info = { identifier } // TODO

author_info = { word+ ~ email? ~ &NEWLINE }

email = { "<" ~ (LETTER | "." )+ ~ "@" ~ (LETTER | "." )+ ~ ">" }

title_block = { anchor* ~ title }
title = {
  (line ~ NEWLINE ~ setext_title_style ) |
  (atx_title_style ~ line)
}
setext_title_style = { ("="{4,} | "-"{4,} | "~"{4,} | "^"{4,} ) ~ &NEWLINE }
atx_title_style = { "="+ }

paragraph = ${ (inline | other_inline)+ }

list = { bullet_list | numbered_list | labeled_list }

list_element = ${
  (
    list_paragraph |
    (continuation ~ delimited_block)
  )+
}
list_paragraph = ${ (inline | other_list_inline)+ }
other_list_inline = @{ (!empty_lines ~ !EOI ~ !inline ~ !(NEWLINE ~ bullet) ~ !(continuation ~ delimited_block) ~ ANY)+ }

bullet = { ("*"+ | "-"+) }
bullet_list_element = { bullet ~ list_element ~ (NEWLINE | EOI) }
bullet_list = { bullet_list_element+ }

numbered_list = { "numbered_list" } // TODO

label_bullet = { (!"::" ~ linechar) ~ "::" }
labeled_list = { (label_bullet ~ list_element)+ }

image_block = { anchor* ~ image }
image = { "image::" ~ (url | path) ~ inline_attribute_list }

attribute_entry = { ":" ~ identifier ~ ":" ~ identifier? ~ &NEWLINE }
attribute_entry_block = { attribute_entry ~ NEWLINE }

delimited_block = {
  (anchor | attribute_list | blocktitle)* ~
  (
    //delimited_admonition |
    delimited_comment |
    //delimited_example |
    //delimited_fenced |
    //delimited_listing |
    delimited_literal |
    //delimited_open |
    //delimited_passthrough |
    //delimited_quote |
    //delimited_sidebar |
    delimited_source |
    //delimited_stem |
    //delimited_table |
    delimited_verse
  )
}

delimited_inner = @{ (!(NEWLINE ~ PEEK) ~ ANY)* }

delimited_comment = {
  PUSH("/"{4,}) ~ NEWLINE ~
  delimited_inner ~
  NEWLINE ~ POP ~ &NEWLINE
}

delimited_literal = {
  PUSH("."{4,}) ~ NEWLINE ~
  delimited_inner ~
  NEWLINE ~ POP ~ &NEWLINE
}

delimited_source = {
  PUSH("-"{4,}) ~ NEWLINE ~
  delimited_inner ~
  NEWLINE ~ POP ~ &NEWLINE
}

delimited_verse = { "verse" } // TODO

include_macro = { "include::" ~ path ~ inline_attribute_list }

////////////////////////////////////////////////////////////////////////////////
// often resused elements

anchor = { inline_anchor ~ NEWLINE }
inline_anchor = { "[[" ~ (identifier | path) ~ "]]" }
attribute_list = { inline_attribute_list ~ NEWLINE }

inline_attribute_list = {
  "[" ~ (attribute ~ ("," ~ attribute)* )? ~ "]"
}

attribute = { named_attribute | attribute_value }
named_attribute = { identifier ~ "=" ~ attribute_value }
attribute_value = {
  ("\"" ~ inner_attribute_value ~ "\"") |
  ( (!"," ~ !"]" ~ ANY)+ )
}
inner_attribute_value = { ( "\\\"" | (!"\"" ~ ANY))* }
blocktitle = { "." ~ !"." ~ line ~ NEWLINE }

////////////////////////////////////////////////////////////////////////////////
// inline elements

inline = {
  !empty_lines ~ !EOI ~
  ( comment |
    link |
    strong |
    emphasized |
    monospaced |
    quoted |
    footnote |
    footnoteref |
    xref
  )
}
other_inline = @{ (!empty_lines ~ !EOI ~ !inline ~ ANY)+ }

link = ${ url ~ inline_attribute_list }
url = ${proto ~ "://" ~ path}
proto = ${ ("http" ~ "s"?) |
           "mailto" |
           "git"
         }

xref = !{ "<<" ~ identifier ~ (NEWLINE? ~ "," ~ NEWLINE? ~ word+)? ~ ">>" }

footnote = { "footnote:" ~ inline_attribute_list }
footnoteref = { "footnoteref:" ~ inline_attribute_list }

strong = ${ "*" ~ (!"*" ~ linechar)+ ~ "*" }
emphasized = @{ "_" ~ (!"_" ~ linechar)+ ~ "_" }
monospaced = ${ inline_anchor* ~ (("+" ~ (!"+" ~ linechar)+ ~ "+") | ("`" ~ (!"`" ~ linechar)+ ~ "`")) }
quoted = @{ inline_attribute_list ~ "#" ~ (!"#" ~ linechar)+ ~ "#" }

// TODO Damit werden keine Kommentare zu Beginn eines Paragraphen angezeigt
comment = { NEWLINE ~ "//" ~ (!NEWLINE ~ ANY)* ~ &NEWLINE }

continuation = { NEWLINE ~ "+" ~ NEWLINE }

////////////////////////////////////////////////////////////////////////////////
// generics

// TODO Should all unicode letters be matched? Instead of just ascii?
identifier = @{ (ASCII_ALPHANUMERIC | "_") ~ (ASCII_ALPHANUMERIC | "_" | "-" | ".")* }

word = @{ (LETTER | NUMBER | "_" | "-")+ }
path = @{ (LETTER | NUMBER | "_" | "-" | "." | "/" | "~" )+ }

linechar = { (!NEWLINE ~ ANY) }

line = { linechar+ ~ ( &NEWLINE | &EOI) }

empty_lines = _{ NEWLINE{2, } | (NEWLINE ~ EOI) }

////////////////////////////////////////////////////////////////////////////////

// Implicit whitespace rule
WHITESPACE = _{ " " | "\t" }
